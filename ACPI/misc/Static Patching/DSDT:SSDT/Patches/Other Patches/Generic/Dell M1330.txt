# Dell M1330 (Intel X3100 video)
#
into device name_adr 0x001B0000 set_label
begin
HDEF
end;
into_all all code_regex AZAL replaceall_matched
begin
HDEF
end;
into method label _DSM parent_adr 0x001B0000 remove_entry;
into device name_adr 0x001B0000 insert
begin
Method (_DSM, 4, NotSerialized)\n
{\n
    Store (Package (0x04)\n
        {\n
            "codec-id", \n
            Buffer (0x04)\n
            {\n
                0x16, 0x76, 0x84, 0x83\n
            }, \n
            "layout-id", \n
            Buffer (0x04)\n
            {\n
                0x0C, 0x00, 0x00, 0x00\n
            }\n
        }, Local0)\n
    DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
    Return (Local0)\n
}
end;
into device name_adr 0x001B0000 code_regex Name\s\(_PRW,\sPackage\s\(0x02\)\s*\n\s*\{\s*\n(?:\s*0x[0-9A-F]+,?\s*\n)+\s*\}\) remove_matched;
into method label _DOD parent_adr 0x00020000 code_regex (Return\s\(Package\s\()0x04(\)\s*\{\s*\n)([^\n,]+),\s*\n[^\n]+\n([^\n]+\n)[^\n]+\n replaceall_matched begin %10x02%2%4%3\n end;
into method label _DSM parent_adr 0x00020000 remove_entry;
into device name_adr 0x00020000 insert
begin
Method (_DSM, 4, NotSerialized)\n
{\n
    Store (Package (0x04)\n
        {\n
            "AAPL,HasPanel", \n
            Buffer (0x04)\n
            {\n
                0x01, 0x00, 0x00, 0x00\n
            }, \n
            "model", \n
            Buffer (0x06)\n
            {\n
                "X3100"\n
            }\n
        }, Local0)\n
    DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
    Return (Local0)\n
}
end;
into device label TV parent_adr 0x00020000 remove_entry;
into device label DVI parent_adr 0x00020000 remove_entry;
into device name_hid PNP0B00 code_regex IRQNoFlags\s\(\)\n\s+\{(\d+)\} remove_matched;
into device name_hid PNP0103 code_regex_not IRQNoFlags code_regex Name\s\(([^,]+),\sResourceTemplate\s\(\)\n\s+\{((?:.|\n)*)\}\) replace_matched
begin
Name (%1, ResourceTemplate ()\n
                    {\n
                        IRQNoFlags ()\n
                            {0}\n
                        IRQNoFlags ()\n
                            {8}\n
%2
})
end;
into method label DTGP remove_entry;
into definitionblock code_regex . insert
begin
Method (DTGP, 5, NotSerialized)\n
{\n
    If (LEqual (Arg0, Buffer (0x10)\n
            {\n
                /* 0000 */    0xC6, 0xB7, 0xB5, 0xA0, 0x18, 0x13, 0x1C, 0x44, \n
                /* 0008 */    0xB0, 0xC9, 0xFE, 0x69, 0x5E, 0xAF, 0x94, 0x9B\n
            }))\n
    {\n
        If (LEqual (Arg1, One))\n
        {\n
            If (LEqual (Arg2, Zero))\n
            {\n
                Store (Buffer (One)\n
                    {\n
                        0x03\n
                    }, Arg4)\n
                Return (One)\n
            }\n
            If (LEqual (Arg2, One))\n
            {\n
                Return (One)\n
            }\n
        }\n
    }\n
    Store (Buffer (One)\n
        {\n
            0x00\n
        }, Arg4)\n
    Return (Zero)\n
}
end;
into_all all code_regex (Name\s\(_HID,\s\")\*?([^\"]+\*?\"\)) replaceall_matched begin %1%2 end
