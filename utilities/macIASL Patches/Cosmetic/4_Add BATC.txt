# Patch Name    : Add BATC
# Patch Version : 1.0

# Add the missing BATC device
into device label BATC parent_label EC remove_entry;
into device label EC insert begin
Device (BATC)\n
{\n
    Name (_HID, EisaId ("PNP0C0A"))  // _HID: Hardware ID\n
    Name (_UID, 0x02)  // _UID: Unique ID\n
    Method (_INI, 0, NotSerialized)  // _INI: Initialize\n
    {\n
        ^^BAT0._HID = Zero\n
        ^^BAT1._HID = Zero\n
    }\n
    Method (CVWA, 3, NotSerialized)\n
    {\n
        If (Arg2)\n
        {\n
            Arg0 = ((Arg0 * 0x03E8) / Arg1)\n
        }\n

        Return (Arg0)\n
    }\n

    Method (_STA, 0, NotSerialized)  // _STA: Status\n
    {\n
        Return ((^^BAT0._STA () | ^^BAT1._STA ()))\n
    }\n

    Name (B0CO, Zero)\n
    Name (B1CO, Zero)\n
    Name (B0DV, Zero)\n
    Name (B1DV, Zero)\n
    Method (_BST, 0, NotSerialized)  // _BST: Battery Status\n
    {\n
        Local0 = ^^BAT0._BST ()\n
        Local2 = ^^BAT0._STA ()\n
        If ((0x1F == Local2))\n
        {\n
            Local4 = DerefOf (Local0 [0x02])\n
            If ((!Local4 || (Ones == Local4)))\n
            {\n
                Local2 = Zero\n
            }\n
        }\n

        Local1 = ^^BAT1._BST ()\n
        Local3 = ^^BAT1._STA ()\n
        If ((0x1F == Local3))\n
        {\n
            Local4 = DerefOf (Local1 [0x02])\n
            If ((!Local4 || (Ones == Local4)))\n
            {\n
                Local3 = Zero\n
            }\n
        }\n

        If (((0x1F != Local2) && (0x1F == Local3)))\n
        {\n
            Local0 = Local1\n
            Local2 = Local3\n
            Local3 = Zero\n
        }\n

        If (((0x1F == Local2) && (0x1F == Local3)))\n
        {\n
            Local4 = DerefOf (Local0 [Zero])\n
            Local5 = DerefOf (Local1 [Zero])\n
            If (((Local4 == 0x02) || (Local5 == 0x02)))\n
            {\n
                Local0 [Zero] = 0x02\n
            }\n
            ElseIf (((Local4 == One) || (Local5 == One)))\n
            {\n
                Local0 [Zero] = One\n
            }\n
            ElseIf (((Local4 == 0x05) || (Local5 == 0x05)))\n
            {\n
                Local0 [Zero] = 0x05\n
            }\n
            ElseIf (((Local4 == 0x04) || (Local5 == 0x04)))\n
            {\n
                Local0 [Zero] = 0x04\n
            }\n

            Local0 [One] = (CVWA (DerefOf (Local0 [One]), B0DV, B0CO) + CVWA (DerefOf (Local1 [One]), B1DV, B1CO))\n
            Local0 [0x02] = (CVWA (DerefOf (Local0 [0x02]), B0DV, B0CO) + CVWA (DerefOf (Local1 [0x02]), B1DV, B1CO))\n
            Local0 [0x03] = ((DerefOf (Local0 [0x03]) + DerefOf (Local1 [0x03])) / 0x02)\n
        }\n

        Return (Local0)\n
    }\n

    Method (_BIF, 0, NotSerialized)  // _BIF: Battery Information\n
    {\n
        Local0 = ^^BAT0._BIF ()\n
        Local2 = ^^BAT0._STA ()\n
        If ((0x1F == Local2))\n
        {\n
            Local4 = DerefOf (Local0 [One])\n
            If ((!Local4 || (Ones == Local4)))\n
            {\n
                Local2 = Zero\n
            }\n

            Local4 = DerefOf (Local0 [0x02])\n
            If ((!Local4 || (Ones == Local4)))\n
            {\n
                Local2 = Zero\n
            }\n

            Local4 = DerefOf (Local0 [0x04])\n
            If ((!Local4 || (Ones == Local4)))\n
            {\n
                Local2 = Zero\n
            }\n
        }\n

        Local1 = ^^BAT1._BIF ()\n
        Local3 = ^^BAT1._STA ()\n
        If ((0x1F == Local3))\n
        {\n
            Local4 = DerefOf (Local1 [One])\n
            If ((!Local4 || (Ones == Local4)))\n
            {\n
                Local3 = Zero\n
            }\n

            Local4 = DerefOf (Local1 [0x02])\n
            If ((!Local4 || (Ones == Local4)))\n
            {\n
                Local3 = Zero\n
            }\n

            Local4 = DerefOf (Local1 [0x04])\n
            If ((!Local4 || (Ones == Local4)))\n
            {\n
                Local3 = Zero\n
            }\n
        }\n

        If (((0x1F != Local2) && (0x1F == Local3)))\n
        {\n
            Local0 = Local1\n
            Local2 = Local3\n
            Local3 = Zero\n
        }\n

        If (((0x1F == Local2) && (0x1F == Local3)))\n
        {\n
            B0CO = !DerefOf (Local0 [Zero])\n
            B1CO = !DerefOf (Local1 [Zero])\n
            Local0 [Zero] = One\n
            B0DV = DerefOf (Local0 [0x04])\n
            B1DV = DerefOf (Local1 [0x04])\n
            Local0 [One] = (CVWA (DerefOf (Local0 [One]), B0DV, B0CO) + CVWA (DerefOf (Local1 [One]), B1DV, B1CO))\n
            Local0 [0x02] = (CVWA (DerefOf (Local0 [0x02]), B0DV, B0CO) + CVWA (DerefOf (Local1 [0x02]), B1DV, B1CO))\n
            Local0 [0x04] = ((B0DV + B1DV) / 0x02)\n
            Local0 [0x05] = (CVWA (DerefOf (Local0 [0x05]), B0DV, B0CO) + CVWA (DerefOf (Local1 [0x05]), B1DV, B1CO))\n
            Local0 [0x06] = (CVWA (DerefOf (Local0 [0x06]), B0DV, B0CO) + CVWA (DerefOf (Local1 [0x06]), B1DV, B1CO))\n
        }\n

        Return (Local0)\n
    }\n
}\n

end;